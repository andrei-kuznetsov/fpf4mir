import ru.spbstu.icc.kspt.kuznetsov.fpf4mir.core.facts.*;
import ru.spbstu.icc.kspt.kuznetsov.fpf4mir.core.facts.activity.*;
import ru.spbstu.icc.kspt.kuznetsov.fpf4mir.core.requestfacts.*;
import ru.spbstu.icc.kspt.kuznetsov.fpf4mir.core.facts.build.*;
import ru.spbstu.icc.kspt.kuznetsov.fpf4mir.core.facts.build.maven.*;
import ru.spbstu.icc.kspt.kuznetsov.fpf4mir.core.actionfacts.*;


rule "Assert user action ref (propagation)"
		salience 10000
	when
		$activity : Activity( )
		$ua : UserAction( activity == $activity )
	then
		UserActionRef uar = new UserActionRef($ua);
		insertLogical(uar);
end

rule "Suspend activity for user action (propagation)"
		salience -10000
	when
		$activity : Activity( )
		exists UserAction( activity == $activity )
	then
		System.out.println("**** activity:" + $activity);
		System.out.println("**** user action(s)");
		
		GenericActivitySuspended activityStatus = new GenericActivitySuspended($activity, "Activity suspended due to user action(s) ");
		insertLogical(activityStatus);
end

rule "Suspend request for suspended activity (propagation)"
		salience -10000
	when
		$request : ReqNewActivity( )
		$activity : Activity( request == $request )
		exists ActivitySuspended( activity == $activity )
	then
		insertLogical(new RequestSuspended($request, "Suspended because nested activity is suspended"));
end

rule "Suspend activity for suspended subrequest (propagation)"
		salience -10000
	when
		$activity : Activity(  )
		$request : ReqNewActivity( parentActivity == $activity )
		exists RequestSuspended( request == $request )
	then
		System.out.println("**** activity:" + $activity);
		System.out.println("**** request:" + $request);
		
		insertLogical(new GenericActivitySuspended($activity, "Suspended because subrequest is suspended"));
end