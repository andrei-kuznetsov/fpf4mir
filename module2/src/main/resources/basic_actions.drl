import ru.spbstu.icc.kspt.kuznetsov.fpf4mir.core.facts.*;
import ru.spbstu.icc.kspt.kuznetsov.fpf4mir.core.requestfacts.*;
import ru.spbstu.icc.kspt.kuznetsov.fpf4mir.core.facts.build.*;
import ru.spbstu.icc.kspt.kuznetsov.fpf4mir.core.facts.build.maven.*;
import ru.spbstu.icc.kspt.kuznetsov.fpf4mir.core.actionfacts.*;


rule "Activity_New"
	salience -10000
	when
		$rr : ReqNewActivity(  )
		$list : java.util.LinkedList(  ) from collect ( Activity( request == $rr ) )
		forall ( $b : Activity( request == $rr )
	             ActivityFailed( activity == $b ) 
 		         ActivityErrorFixed( activity == $b ) )
	then
		String pkg = $rr.getClass().getPackage().toString();
		String type = $rr.getClass().getSimpleName();
		type = type.replaceAll("ReqNew","").replaceAll("Req", "") + "Activity";
		
		// TODO: package name?
		pkg = "defaultpkg";
		System.out.println("## pkg: " + pkg);
		System.out.println("## type: " + type);
		System.out.println("## activities (" + $list.size() + "): " + new java.util.HashSet($list));
		
		org.drools.definition.type.FactType factType = drools.getKnowledgeRuntime().getKnowledgeBase().getFactType(pkg, type);
		Object fact = factType.newInstance();
		factType.set(fact, "request", $rr);
		insert( fact );
end

rule "Activity Request Failure Status"
	salience -10001 // salience shall be less than for rule "Activity_New"
	when
		$request : ReqNewActivity( )
		$activity : Activity( request == $request )
		ActivityCompleted( activity == $activity)
		$failure : ActivityFailed( activity == $activity )
		not ActivityErrorFixed(activity == $activity)
	then
		insertLogical(new RequestFailed($request, $failure.getMessage()));
end

rule "Assert user action ref (propagation)"
	when
		$activity : Activity( )
		$ua : UserAction( activity == $activity )
	then
		UserActionRef uar = new UserActionRef($ua);
		insertLogical(uar);
end

rule "Suspend activity for user action (propagation)"
	when
		$activity : Activity( )
		$uar : UserActionRef( activity == $activity )
	then
		GenericActivitySuspended activityStatus = new GenericActivitySuspended($activity, "Activity suspended due to user action: " + $uar);
		insertLogical(activityStatus);
		insertLogical(new GenericAlias().reset(activityStatus, $uar));
end

rule "Suspend request for suspended activity (propagation)"
	when
		$request : ReqNewActivity( )
		$activity : Activity( request == $request )
		exists ActivitySuspended( activity == $activity )
	then
		insertLogical(new RequestSuspended($request, "Suspended because nested activity is suspended"));
end

rule "Suspend activity for suspended subrequest (propagation)"
	when
		$activity : Activity(  )
		$request : ReqNewActivity( parentActivity == $activity )
		exists RequestSuspended( request == $request )
	then
		insertLogical(new GenericActivitySuspended($activity, "Suspended because subrequest is suspended"));
end