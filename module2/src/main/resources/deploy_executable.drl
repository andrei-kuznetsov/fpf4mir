import ru.spbstu.icc.kspt.kuznetsov.fpf4mir.core.facts.*;
import ru.spbstu.icc.kspt.kuznetsov.fpf4mir.core.facts.generic.*;
import ru.spbstu.icc.kspt.kuznetsov.fpf4mir.core.facts.deploy.*;
import ru.spbstu.icc.kspt.kuznetsov.fpf4mir.core.requestfacts.*;
import ru.spbstu.icc.kspt.kuznetsov.fpf4mir.core.facts.build.*;
import ru.spbstu.icc.kspt.kuznetsov.fpf4mir.core.facts.build.maven.*;
import ru.spbstu.icc.kspt.kuznetsov.fpf4mir.core.actionfacts.*;

// TODO: populate
rule "Deploy executable from file artifact"
	when
		$r : ReqDeployExecutable(  )
		$a : DeployExecutableActivity( request == $r )
		$file : FileArtifactAlias( request == $r )
	then
		ReqDeployFolder rdf = new ReqDeployFolder($a);
		FileArtifactAlias f = new GenericFileArtifactAlias($file, rdf);
		insert( f );
		insert( rdf );
end

/*
use deployed folder as is when FolderArtifactAlias( request == $r )
*/

rule "Deploy executable from URL"
	when
		$r : ReqDeployExecutable(  )
		$a : DeployExecutableActivity( request == $r )
		$ref : ArtifactRefAlias( request == $r )
	then
		ReqDeployFolder rdf = new ReqDeployFolder($a);
		ArtifactRefAlias f = new GenericArtifactRefAlias(rdf, $ref);
		insert( f );
		insert( rdf );
end

rule "Propagate deploy folder"
	when
		$r : ReqDeployExecutable(  )
		$a : DeployExecutableActivity( request == $r )
		$rdf : ReqDeployFolder( activity == $a )
		$rdf_stat : RequestStatus( request == $rdf, succeeded == true )
		$folder : FolderArtifactAlias( request == $rdf )
	then
		insertLogical( new GenericFolderArtifactAlias($r, $folder) );
end

rule "Evaluate run executable"
	when
		$r : ReqDeployExecutable(  )
		$a : DeployExecutableActivity( request == $r )
		$folder : FolderArtifactAlias( request == $r )
	then
		ReqNewActivity r = new GenericReqNewActivity(R.activity.GuessRunExecutable, $a);
		FolderArtifactAlias f = new GenericFolderArtifactAlias(r, $folder);
		insert( f );
		insert( r );
end

rule "Propagate run executable"
	when
		$r : ReqDeployExecutable(  )
		$a : DeployExecutableActivity( request == $r )
		$rna : ReqNewActivity( activityName == R.activity.GuessRunExecutable, activity == $a )
		$rna_stat : RequestStatus( request == $rna, succeeded == true )
		$exec : ExecutableFileArtifactAlias( request == $rna )
	then
		insertLogical( new ExecutableFileArtifactAlias($exec) );
end